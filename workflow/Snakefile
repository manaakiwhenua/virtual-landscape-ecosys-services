configfile: "config/config.yaml"

PERMUTATIONS = {
    "landscape": config["landscapes"],
    "topography": config["topographies"],
    "landclass": config["landclasses"]
}
carbon_stocks = expand("results/carbon_stocks/{landscape}/{topography}.{landclass}.LandscapeCarbonStock.tif", **PERMUTATIONS)
greenhouse_gas_emissions = expand("results/greenhouse_gas_emissions/{landscape}/{topography}.{landclass}.LandscapeGreenhouseGasEmissions.tif", **PERMUTATIONS)
pollinator_overlap = expand("results/pollination/{landscape}/{topography}.{landclass}.LandscapePollinatorOverlap.tif", **PERMUTATIONS)
erosion = expand("results/erosion/{landscape}/{topography}.{landclass}.LandscapeErosion_t_km_yr.tif", **PERMUTATIONS)
recreation = expand("results/recreation/{landscape}/{topography}.{landclass}.RecreationValueAdditive.tif", **PERMUTATIONS)

erosion_stats = expand("results/erosion/{landscape}/{topography}.{landclass}.LandscapeErosion_t_km_yr.json", **PERMUTATIONS)
recreation_stats = expand("results/recreation/{landscape}/{topography}.{landclass}.RecreationValueAdditive.json", **PERMUTATIONS)

include: "rules/pollination.smk"
include: "rules/carbon.smk"
include: "rules/ghg.smk"
include: "rules/erosion.smk"
include: "rules/recreation.smk"
include: "rules/ndr.smk"

rule landscape_precipitation:
    input:
        "/cifs/Tlinc/Projects K-O/MCSL/virtual-landscapes/{landscape}/dem_{topography}.tif"
    output:
        "results/precipitation/{landscape}/{topography}.precipitation.tif"
    params:
        dem="/virtual-landscapes/{landscape}/dem_{topography}.tif",
        dem_offset="800",
        datatype="Float64"
    container:
        "docker://osgeo/gdal:ubuntu-small-latest"
    shell:
        'gdal_calc.py -A {params.dem} --outfile={output} --calc="A+{params.dem_offset}" --type={params.datatype} --overwrite --co TILED=YES'


rule all:
    input:
        carbon_stocks,
        greenhouse_gas_emissions,
        pollinator_overlap,
        erosion,
        recreation
