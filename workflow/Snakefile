configfile: "config/config.yaml"

PERMUTATIONS = {
    "landscape": config["landscapes"],
    "topography": config["topographies"],
    "proportion": config["proportions"],
    "landclass": config["landclasses"]
}
carbon_stocks = expand("results/carbon_stocks/{landscape}/{topography}.{landclass}.{proportion}.LandscapeCarbonStock.tif", **PERMUTATIONS)
greenhouse_gas_emissions = expand("results/greenhouse_gas_emissions/{landscape}/{topography}.{landclass}.{proportion}.LandscapeGreenhouseGasEmissions.tif", **PERMUTATIONS)
pollinator_overlap = expand("results/pollination/{landscape}/{topography}.{landclass}.{proportion}.LandscapePollinatorOverlap.tif", **PERMUTATIONS)
erosion = expand("results/erosion/{landscape}/{topography}.{landclass}.{proportion}.LandscapeErosion_t_km_yr.tif", **PERMUTATIONS)
recreation = expand("results/recreation/{landscape}/{topography}.{landclass}.{proportion}.RecreationValueAdditive.tif", **PERMUTATIONS)
ndr = expand("results/ndr/{landscape}/{topography}.{landclass}.{proportion}/n_export.tif", **PERMUTATIONS)
ndr_watershed = expand("results/ndr/{landscape}/{topography}.{landclass}.{proportion}/watershed_results_ndr.shp", **PERMUTATIONS)

ndr_intermediate_modified_load_n = expand("results/ndr/{landscape}/{topography}.{landclass}.{proportion}/intermediate_outputs/modified_load_n.tif", **PERMUTATIONS)

landscapes = expand(f"{config['basepath']}"+"/virtual-landscapes/{landscape}/{landscape}-landclass_{topography}_{landclass}_{proportion}.tif", **PERMUTATIONS)

rule landscape_precipitation:
    input:
        f"{config['basepath']}"+"/virtual-landscapes/{landscape}/{landscape}-dem_{topography}.tif"
    output:
        "results/precipitation/{landscape}/{topography}.precipitation.tif"
    params:
        dem="/virtual-landscapes/{landscape}/{landscape}-dem_{topography}.tif",
        dem_offset="800",
        datatype="Float64"
    container:
        "docker://osgeo/gdal:ubuntu-small-latest"
    log:
        "logs/precipitation/{landscape}/{topography}.precipitation.log"
    shell:
        'gdal_calc.py -A {params.dem} --outfile={output} --calc="A+{params.dem_offset}" --type={params.datatype} --overwrite --co TILED=YES > {log} 2>&1'

include: "rules/pollination.smk"
include: "rules/carbon.smk"
include: "rules/ghg.smk"
include: "rules/erosion.smk"
include: "rules/recreation.smk"
include: "rules/ndr.smk"

rule all:
    input:
        carbon_stocks,
        greenhouse_gas_emissions,
        pollinator_overlap,
        erosion,
        recreation,
        ndr, ndr_watershed

rule summary:
    input:
        erosion,
        recreation,
        pollinator_overlap,
        greenhouse_gas_emissions,
        carbon_stocks,
        ndr, ndr_watershed
    output:
        "results/summary/summary.xlsx"
    conda:
        "envs/rasterio.yml"
    params:
        iterator_sequence=["landscape", "topography", "landclass", "proportion"],
        landscape=config["landscapes"],
        topography=config["topographies"],
        proportion=config["proportions"],
        landclass=config["landclasses"],
        resolution=config["RESOLUTION"],
        erosion=erosion,
        recreation=recreation,
        pollinator_overlap=pollinator_overlap,
        greenhouse_gas_emissions=greenhouse_gas_emissions,
        carbon_stocks=carbon_stocks,
        landscapes=landscapes,
        ndr=ndr,
        ndr_intermediate_modified_load_n=ndr_intermediate_modified_load_n
    log:
        "logs/summary/summary.log"
    script:
        "scripts/summary.py"
